program = _{ SOI ~ expr ~ EOI }

keyword = { "bind" | "in" }

expr     =  { bind* ~ output }
bind     =  { "bind" ~ var_list ~ "=" ~ value ~ "in" }
output   = _{ value | "(" ~ (value ~ ("," ~ value)+)? ~ ")" }
var_list = _{ variable | "(" ~ (variable ~ ("," ~ variable)*) ~ ")" }

thunk = { variable* ~ "." ~ expr }

value = { thunk | variable | op ~ ("(" ~ value ~ ("," ~ value)* ~ ")")? }

op     =  { "plus" | "minus" | "times" | "div" | "rem" | "and" | "or" | "not" | "if" | "eq" | "neq" | "lt" | "leq" | "gt" | "geq" | "app" | "lambda" | "atom" | "deref" | "assign" | "tuple" | "detuple" | "true" | "false" | number }
number = @{ ASCII_DIGIT+ }

variable = @{ !(keyword | op) ~ "_"* ~ ASCII_ALPHA ~ (ASCII_ALPHANUMERIC | "_")* }

COMMENT    = _{ "#" ~ (!"\n" ~ ANY)* }
newline    = _{ "\n" | "\r\n" }
WHITESPACE = _{ " " | "\t" | newline }
